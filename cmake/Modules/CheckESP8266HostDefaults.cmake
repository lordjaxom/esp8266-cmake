if(CMAKE_HOST_SYSTEM_NAME MATCHES "Linux")
    set(ESP8266_DEFAULT_ARDUINO_USER "$ENV{HOME}/.arduino15")
    set(ESP8266_EXEC_SUFFIX "")
elseif(CMAKE_HOST_SYSTEM_NAME MATCHES "Windows")
    if(IS_DIRECTORY "$ENV{ProgramFiles\(x86\)}/Arduino")
        set(ESP8266_DEFAULT_ARDUINO_HOME "$ENV{ProgramFiles\(x86\)}/Arduino")
    elseif(IS_DIRECTORY "$ENV{ProgramFiles}/Arduino")
        set(ESP8266_DEFAULT_ARDUINO_HOME "$ENV{ProgramFiles}/Arduino")
    endif()
    set(ESP8266_DEFAULT_ARDUINO_USER "$ENV{LOCALAPPDATA}/Arduino15")
    set(ESP8266_EXEC_SUFFIX ".exe")
else()
    message(FATAL_ERROR "Unsupported build platform ${CMAKE_HOST_SYSTEM_NAME}")
endif()

if("${ARDUINO_HOME}" STREQUAL "")
    if(NOT "$ENV{ARDUINO_HOME}" STREQUAL "")
        set(ARDUINO_HOME "$ENV{ARDUINO_HOME}" CACHE PATH "Path to Arduino installation")
    else()
        set(ARDUINO_HOME "${ESP8266_DEFAULT_ARDUINO_HOME}" CACHE PATH "Path to Arduino installation")
    endif()
endif()
if(NOT "${ARDUINO_HOME}" STREQUAL "")
    message(STATUS "Using ARDUINO_HOME ${ARDUINO_HOME}")
else()
    message(FATAL_ERROR "ARDUINO_HOME is not set and could not be guessed")
endif()

if("${ARDUINO_USER}" STREQUAL "")
    if(NOT "$ENV{ARDUINO_USER}" STREQUAL "")
        set(ARDUINO_USER "$ENV{ARDUINO_USER}" CACHE PATH "Path to Arduino user preferences")
    else()
        set(ARDUINO_USER "${ESP8266_DEFAULT_ARDUINO_USER}" CACHE PATH "Path to Arduino user preferences")
    endif()
endif()
if(NOT "${ARDUINO_USER}" STREQUAL "")
    message(STATUS "Using ARDUINO_USER ${ARDUINO_USER}")
else()
    message(FATAL_ERROR "ARDUINO_USER is not set and could not be guessed")
endif()

if(EXISTS "${ARDUINO_USER}/preferences.txt")
    file(STRINGS "${ARDUINO_USER}/preferences.txt" SKETCHBOOK_LINE REGEX "^sketchbook.path=")
    if(NOT "${SKETCHBOOK_LINE}" STREQUAL "")
        string(REGEX REPLACE "^sketchbook.path=" "" SKETCHBOOK_PATH "${SKETCHBOOK_LINE}")
        set(ARDUINO_SKETCHBOOK "${SKETCHBOOK_PATH}" CACHE PATH "Path to the sketchbook")
        message(STATUS "Using ARDUINO_SKETCHBOOK ${ARDUINO_SKETCHBOOK}")
    endif()
endif()
